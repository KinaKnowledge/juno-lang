
;; main browser initialization file - initializes the user browser environment
;; after the browser_workspace has setup the underlying environment

(defglobal main_menu_button nil) ;; placeholders 
(defglobal resource_loader_button nil)
(defglobal juno_container nil)
(defglobal repl_editor nil)
(defglobal *scratch* ";; *scratch* buffer\n\n")

;; $controls is the list of global controls such as editors and views
;; putting $ as a prefix indicates to the environment that it is not
;; to be saved as part of the image.  It must be rebuilt on initialization
;; since it contains live stateful objects.
(console.log "browser_initializer running")
(defglobal $controls nil)

(defun make_menu (for_element menu_items)
  (let
      ((menu_items menu_items)
       (for_element for_element)
       (pos nil)
       (disappear_timer nil)
       (view nil))
    (aif (get_by_id "cmenu")
         (-> it `remove))
    (setq view
          (div { `id: `cmenu `class: "juno-menu"  }
               (div {  `class: "pure-menu pure-menu-horizontal"  }               
                    (ul { `class: "pure-menu-list" }                 
                        (map (fn (item idx)                     
                               (li { `idx: idx `class: "pure-list-item" } (a { `idx: idx  `href: "#" `class: "pure-menu-link" } item.0)))
                             (or menu_items []))))))
    (when for_element
      (setq pos
            (-> for_element `getBoundingClientRect))
      (set_style [["top" (+ "" (Math.max 0 pos.bottom) "px") ]
                  ["left" (+ "" (Math.max pos.left 0) "px") ]] view))
    
    (attach_event_listener view
                           `click
                           (fn (e)
                             (progn
                              (defvar idx (-> e.srcElement `getAttribute `idx))
                              (defvar item (prop menu_items idx))
                             
                              (console.log "clicked item: " idx item item.1)
                               (when item
                                 (item.1 e))
                               (-> view `remove))))
    (setTimeout
     (fn ()
       (progn
        (attach_event_listener view
                               `mouseleave
                               (function (e)
                                         (progn
                                          (when disappear_timer
                                            (clearTimeout disappear_timer))
                                          (= disappear_timer (setTimeout (fn ()
                                                                           (progn
                                                                            (-> view `remove)
                                                                            (= disappear_timer nil)))
                                                                         500)))))
        (attach_event_listener view
                               `mouseenter
                               (function (e)
                                         (when disappear_timer
                                           (clearTimeout disappear_timer)
                                           (= disappear_timer nil))))))
     500)
    (-> (get_by_id "body") `appendChild view) 
    view))

(defun request_user_input (prompt type)
  (new Promise (fn (resolve reject)                 
                 (let
                     ((user_input  (input { `type: (or type "text") }))
                      (ok_button (button { `class: "pure-button-primary" `style: "float:right" } "OK"))
                      (cancel_button (button  "Cancel"))
                      (dwin (dialog prompt                                    
                                    (br)
                                    user_input
                                    (br)
                                    (div { `class: "pure-button-group" `role: "group" }
                                         cancel_button
                                         ok_button))))
                   (attach_event_listener ok_button
                                          `click
                                          (fn (e)
                                            (do                                              
                                              (resolve user_input.value)
                                              (-> dwin `remove))))
                   (attach_event_listener cancel_button
                                          `click
                                          (fn (e)
                                            (do
                                              (-> dwin `remove)
                                              (resolve nil))))
                   (-> juno_container
                       `appendChild dwin)
                   (when (is_function? dwin.showModal)
                     (-> dwin `showModal))))))
                                           
    
                     
(defun message (text)
  (rlog (div { `style: "border-left: 5px solid darkblue; padding: 5px;" } text)))

(defun error_message (text)
  (rlog (div { `style: "border-left: 5px solid red; padding: 5px;" } text)))

(defun log_error (message error_instance)
  (progn
   (error_message message)
   (console.error message)
   (when error_instance
     (when error_instance.messasge
       (error_message error_instance.message))
     (console.error error_instance))))
     

(defun load_js_file (file)
  (let
      ((fdata (try
                (read_file file { `read_as: "text" })
                (catch Error (e)
                  e)))
       (assignment_to nil)
       (error nil))
    (message (+ "found and read JS " file.name ", size: " file.size))
    (cond
      (and fdata
           (not (instanceof fdata Error)))
      (progn
       (aif (request_user_input "Enter a name for a reference to the loaded Javascript" "text")
            (do
              (= assignment_to it)              
              (try
                (progn
                 (debug)
                 (cond
                   (or (== assignment_to "global")
                       (== assignment_to "script"))
                   (-> (page_header) `append (script { `type: "text/javascript" `charset: "utf-8" }
                                                     (javascript fdata)))
                   else
                   (eval `(defglobal ,#assignment_to (javascript ,#fdata)))))
                (catch Error (e)
                  (do
                    (= error true)
                    (error_message (+ "Unable to create " assignment_to ": " e.message))
                    (console.error "ERROR: " e))))                                                    
              (when (not error)
                (message (+ "Loaded JS to " assignment_to ))))
            (notify "Entry Cancelled")))
      else
      (progn
       (rlog "Unable to read the file")
       (console.error fdata)))))

(defun embed_js_orig (name js_text) 
  (progn
   (debug)
   (-> (page_header) `append (html/script { `id: name `type: "text/javascript" `charset: "utf-8" }
                                     (javascript js_text)))   
   (eval `(defglobal ,#name (html/get_by_id ,#name)
            {
             `embedded_script: true
             }))))

(defun embed_js (name js_text) 
  (progn
   (debug)
   (eval `(-> (page_header) `append (html/script { `id: name `type: "text/javascript" `charset: "utf-8" }
                                     (javascript ,#js_text))))
   (eval `(defglobal ,#name (html/get_by_id ,#name)
            {
             `embedded_script: true
             }))))

(defun embed_js_file (file)
  (let
      ((fdata (try
                (read_file file { `read_as: "text" })
                (catch Error (e)
                  e)))
       (assignment_to nil)
       (error nil))
    (message (+ "found and read JS " file.name ", size: " file.size))
    (cond
      (and fdata
           (not (instanceof fdata Error)))
      (progn
       (aif (request_user_input "Enter a name for a reference to the loaded Javascript" "text")
            (do
              (= assignment_to it)              
              (try
                (embed_js_orig assignment_to fdata)                   
                (catch Error (e)
                  (do
                    (= error true)
                    (error_message (+ "Unable to create " assignment_to ": " e.message))
                    (console.error "ERROR: " e))))                                                    
              (when (not error)
                (message (+ "Loaded JS to " assignment_to ))))
            (notify "Entry Cancelled")))
      else
      (progn
       (rlog "Unable to read the file")
       (console.error fdata)))))

(defun load_files (files)
  (when (> files.length 0)
    (for_each (file files)
       (do
         (cond
           (== file.type "text/javascript")
           (embed_js_file file)
           (== file.type "text/plain")
           (notify "Text Resource")
           (or (ends_with? file.name ".lisp")
               (ends_with? file.name ".juno"))
           (notify "Loading lisp file"))))))


(defun make_editor (content options)
  (let
      ((options (or options {
                             `type: "juno"
                             }))
       (resize_observer nil)
       (intf {})
       (content (or content ""))
       (controls (div { `style: "display: flex; " }))
       (editor_div (div { `style: "height: calc(100% - 20px); width: 100%; overflow: hidden;" }))
       (view (div { `style: "overflow: hidden; height: calc(100% - 5px); display: inline-block; width: inherit;" }                  
                  controls
                  editor_div))      
       (perform_resize (fn ()
                         (when view.parentElement
                          ;(console.log "editor: main resize: " (prop view.parentElement `offsetWidth) (prop view.parentElement `offsetHeight))
                          (html/set_style [["width" "calc(100% - 2px)"];           (+ "" (- (prop view.parentElement `offsetWidth) 2) "px")]
                                           ["height" (+ "" (- (prop view.parentElement `offsetHeight) 10) "px")]]
                                          view)
                          (-> editor `resize)
                          )))
       (editor nil)
       (initialize_editor (fn ()
                            (progn
                             (setq editor
                                   (ace.edit editor_div))
                             (when options.evaluator
                               (-> editor.commands `addCommand
                                   {
                                    `name: "execute"
                                    `bindKey: { `win: "Shift-Enter" `mac: "Shift-Enter" }
                                    `exec: (function (editor)
                                                     (let
                                                         ((val nil)
                                                          (copytext (if options.repl_mode
                                                                      (-> editor `getValue)
                                                                      (-> editor `getCopyText))))
                                                       (if (blank? copytext)
                                                         (if (not options.repl_mode)
                                                           (notify (dtext "Please highlight text to evaluate")))
                                                         (try
                                                           (options.evaluator copytext)
                                                           (catch Error (e)
                                                             (progn
                                                              (notify (+ (dtext "Unhandled error") ": " e.message ))
                                                              (console.error e)))))))
                                    }))
                             (when options.on_save
                               (-> editor.commands `addCommand
                                   {
                                    `name: "save"
                                    `bindKey: { `win: "Ctrl-s" `mac: "Command-s" }
                                    `exec: (function (editor)
                                                     (options.on_save (-> editor `getValue)))
                                    }))

                             
                             
                             (when (is_array? options.commands)
                               (for_each (command options.commands)
                                  (do
                                    (-> editor.commands `addCommand
                                        command))))
                                              
                             (when options.on_focus
                               (-> editor `on `focus options.on_focus))
                             (when options.on_blur
                               (-> editor `on `blur options.on_blur))
                             (when options.read_only
                               (-> editor `setReadOnly true))
                             (-> editor.session `setUseSoftTabs true)
                             (-> editor `setOptions {
                                   `enableBasicAutocompletion: true
                                   `enableLiveAutocompletion: true
                                   `showFoldWidgets: true
                                   `fontFamily: "Roboto Mono"                  
                                   })
                             (-> editor.session `setUseWorker true)
                             (-> editor `setShowFoldWidgets true)
                             (cond
                               (== options.type "juno")
                               (-> editor.session `setMode "ace/mode/juno")
                               (== options.type "javascript")
                               (-> editor.session `setMode "ace/mode/javascript")
                               (== options.type "css")
                               (-> editor.session `setMode "ace/mode/css"))
                             (console.log "editor initialized: content: " (subtype content) content)                                                          
                             (-> editor `setValue content -1)
                             
                             (when options.on_change
                               (-> editor `on `change (function (val)
                                                                (options.on_change editor val))))                             
                             (-> editor `resize true)                             
                             (set_prop intf `editor editor)                             
                             ))))
       
    
    (if (not (is_symbol? `$ace_editor))
      (throw TypeError "$ace_editor symbol not found, and is required for this editor."))
    
    (when (is_element? options.command_bar)
      (-> controls `prepend options.command_bar))
      
    (= resize_observer (new ResizeObserver perform_resize))
    (= intf
       {
        `view: view
        `editor: editor
        `set: (fn (text)
                (-> editor `setValue text))
        `get: (fn (text)
                (-> editor `getValue))
        `options: options
        `initialize: (fn ()
                       (when view.parentElement
                         (console.log "editor: performing initialization")
                         (-> resize_observer `observe view.parentElement)
                         (initialize_editor)
                         ))
        })))
               
(defun show_main_menu ()
  (let
      ((view nil))
    
    (setq view
          (make_menu main_menu_button
                     [["Save Environment" (fn (e) (save_image)) ]
                      ["Load Resource" (fn (e) (-> resource_loader_button `click)) ]
                      ["Style CSS" (fn (e) (notify "View Site CSS"))]
                      ]))))   
    

(defun buffer_editor (title namespace path_to_value mode options)
  (let
      ((nseval (if (not (blank? namespace))
                 (-> Environment `get_namespace_handle namespace)
                 Environment))
       (title_span (span { `class: "juno-title" } title))
       (buffer_commands_button (button { `title: "Buffer Commands" } "â–¸"))
       (is_dirty? false)
       (command_bar (or options.command_bar
                        (div { `class: "juno-buffer-bar" }
                             buffer_commands_button
                             title_span                                                             
                             (span { `title: "Namespace" `class: "juno-namespace" `style: "display:inline-block; padding-left: 30px; padding-right: 5px;" } nseval.namespace)
                             (if options.read_only
                               (span { `title: "This buffer doesn't allow editing" `style: "padding-left: 5px; padding-right: 5px;" } (dtext "Locked"))
                               ())
                             (span { `class: "juno-mode" `title: "Current Editing Mode" `style: "float: right; margin-right: 15px; display: inline-block; padding-left: 5px; padding-right: 5px;" } mode))))      
       (init_value (resolve_path path_to_value nseval.global_ctx.scope))
       (on_save (fn (value)
                  (progn
                   (try
                     (progn
                      (set_path path_to_value nseval.global_ctx.scope
                                value)
                      (remove_class "juno-needs-save" title_span)
                      (= is_dirty? false)
                      (notify (+ "Set " title)))
                     (catch Error (`e)
                       (progn
                        (log_error (+ (dtext "Unable to save buffer")
                                      " "
                                      title)
                                   e)))))))
                     
       (edit_control (make_editor (if (is_string? init_value)
                                    init_value
                                    "")                                  
                                  {
                                   type: mode
                                   command_bar: command_bar
                                   on_focus: (fn ()
                                               (progn
                                                (console.log "focused editor.")
                                                (add_class "juno-focused" title_span)))
                                   on_blur: (fn ()
                                              (remove_class "juno-focused" title_span))
                                   on_save: on_save
                                   read_only: options.read_only
                                   on_change: (function (editor val)
                                                        (progn
                                                         (when (not is_dirty?)
                                                           (= is_dirty? true)
                                                           (add_class "juno-needs-save" title_span))
                                                         (when options.on_change
                                                           (-> options.on_change editor val))))
                                    evaluator: options.evaluator
                                   })))
    (console.log "buffer_editor: init_value: " init_value path_to_value nseval)
    edit_control))


(defun log_view (options)
  (let
      ((output_container (html/div {   `style: "font-size: 0.9em; height: 100%; overflow: scroll; display: flex; flex-direction: column-reverse; width: 100%; vertical-align:bottom; " } ))
       (add_result (fn (result command_text error_state)
                     (progn
                      (when (not (eq nil command_text))
                        (-> output_container
                            `prepend
                            (html/div { style:"border-top: 1px solid #C0C0C0; padding: 5px; margin-top: 1px; background: #F8F6F6; width: calc(100% - 10px); display: inline-block; " }
                                      (html/code { `style: "font-style: italic; color: darkblue;" }
                                                 command_text))))
                      (-> output_container
                          `prepend
                          (html/div { `style: "padding: 5px; margin-top: 0px; background: #F8F6F6; width: calc(100% - 10px); display: inline-block; " }
                                    (cond
                                      (is_element? result)
                                      result
                                      (is_number? result)
                                      (html/code { `style: " color: green;" }
                                                 result)
                                      (or (== true result)
                                          (== false result))
                                      (html/code { `style: " color: orange;" }
                                                 result)
                                      (is_function? result)
                                      (html/code { `style: " color: darkblue;" } "Function: "
                                                 (if result.name result.name "anonymous"))
                                      
                                      (and (is_array? result)
                                           (instanceof result.0 Error))
                                      (html/pre { `style: " color: darkred;" } "ERROR: "
                                                (subtype result.0)
                                                "\n"
                                                result.0.message)
                                      (or error_state
                                          (instanceof result Error))
                                      (html/pre { `style: " color: darkred;" } "ERROR: "
                                                (subtype result)
                                                "\n"
                                                result.message)
                                      (and (is_array? result)
                                           result.0.error)
                                      (html/pre { `style: " color: darkred;" }                                                 
                                                (JSON.stringify result nil 3 ))
                                           
                                      else
                                      (html/pre { `style: "" } (JSON.stringify result nil 3 )))))
                      (when (> output_container.children.length (or options.max_entries 100))
                        (-> (last output_container.children)
                            `remove))
                      (set_prop output_container
                                `scrollTop
                                output_container.offsetHeight)
                      result)))                                                               
       (interf {}))
    (set_prop interf
              `view output_container
              `append add_result
              `options options)
    interf))

(defun browser_repl ()
  (let
      ((output_control (log_view))
       (resize_observer nil)                                                                                            
       (current_env Environment)
       (error_state nil)
       (initialized false)
       (evaluator (fn (lisp_text no_eval)
                    (let
                        ((result nil))
                      (setq error_state nil)
                      (setq current_env (-> Environment `get_namespace_handle (current_namespace)))                      
                      (try
                        (= result (if no_eval
                                    lisp_text
                                    (-> current_env `evaluate lisp_text)))
                        (catch Error (ex)
                          (do
                            (= error_state true)
                            (= result ex))))
                      (console.log "<-" result)
                      (-> output_control `append result lisp_text error_state)
                      (if (not error_state)
                        (-> input_container `set "")))))
                             
       (input_container (make_editor 
                                   ""                                  
                                  {
                                   type: "juno"                                                                                                                             
                                   on_change: (function (editor val)
                                                        (progn
                                                         true))
                                   repl_mode: true
                                   evaluator: evaluator
                                                 
                                   }))
       
       (sview (split_view_horizontal
                                 output_control
                                 input_container
                                 { `ratio: 0.7 } ))  ;; 80/20 top-bottom
       
       (juno_container sview.view)
       
       (initialize (fn ()
                     (progn
                      (if initialized
                        (evaluator (div { `style: "display: flex padding: 5px" } "WARNING: REPL already initialized") true)
                        (progn
                         (= initialized true)                                                 
                         (-> output_control `append (html/div { `style: "display: flex padding: 5px" }                
                                              (html/span { `style: "font-weight: bold; display: inline-block;" } "Juno REPL")
                                              (html/span { `style: "display: inline-block; margin-left: 10px;" }
                                                         "Version: " Environment.build_version   )))
                         (-> output_control `append (html/div "Namespace: " (current_namespace)))))))))
    {
     `view: juno_container
     `input_container: input_container
     `initialize: initialize
     `output_container: output_control.view
     `log: (fn (val)
             (-> output_control `append val))
     `evaluate: (fn (lisp_text)
                  (evaluator lisp_text))
     }))




(defun user/*initializer* ()
  (let
      ((header nil)       
       (scripts (-> (page_header) `querySelectorAll "script[id]"))        
       (embeds  nil)       
       (initial_split nil)
       (repl_split nil))
                                                               
    (console.log "user/*initializer is running." )
   
    (defglobal main_menu_button (li  { `class: "juno-menu-header" }
                                             (span { `class: "juno-menu-item" `style: "" } "File")))

    (defglobal resource_loader_button (file_picker_button "" load_files))    
    (set_style [["position" "absolute"]
                ["top" "-100px"]
                ["left" "-100px"]
                ["width" "1px"]
                ["height" "1px"]] resource_loader_button)
    
    (-> (get_by_id "body") `appendChild resource_loader_button)

    (defglobal $repl (browser_repl))
    (defglobal $controls [])
    
    
    (defglobal juno_container
      (div { `style: "height: inherit; width: inherit; position: relative; overflow:hidden;" }))
    
      
    (setq header (-> document `querySelector "header"))

    (-> header `appendChild (span { `class: "juno-header" } "Seedling" ))
    (-> header `appendChild (ul { `class: "pure-menu-list" `style: "margin-left: 10px;" }
                                main_menu_button))
    
    ;(-> header `appendChild (img { `style: "margin-left: 5px;margin-right: 10px;margin-top: 5px; " src: core/juno_logo height: "15" }))

    (attach_event_listener juno_container `click
                           (fn (e)
                             (progn
                              (defvar menu (get_by_id `cmenu))
                              (when menu (-> menu `remove)))))
    
    (set_prop *env_config*.export
	      `save_path
	      nil)
        
    (attach_event_listener main_menu_button
			      `click
			      (fn (e)
				(show_main_menu)))

    (-> (get_by_id "body") `appendChild juno_container)
    ;(console.log "repl is built")
    ;; make the repl more sophisticated
    
    
    (push $controls
          (buffer_editor "*scratch*" "user" [ `*scratch* ] `juno))
    
    
    ;(-> repl.view `remove)
    (setq initial_split
          (split_view_vertical $controls.0
                               $repl))

    (-> juno_container
        `append initial_split.view)
      
      
    (console.log "*initializer: calling initial_split: initializer")
    (debug)
    (-> initial_split `initialize)
    (when (blank? (prop *env_config*.export `save_prefix))
      (set_prop *env_config*.export
                `save_prefix
                "seedling"))
  (notify (+ "Hello " *namespace* ))
  true))





